apply plugin: 'com.android.application'
//apply plugin: 'kotlin-android-extensions'
//apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "ru.vasiliev.sandbox"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        sourceSets.main {
            jni.srcDirs = [] // Disable automatic ndk-build call
            jniLibs.srcDir 'src/main/jniLibs' // JNI libs location
        }

        externalNativeBuild {
            cmake {
                cppFlags "-DWITH_LIVENESS -std=c++11 -frtti"
                arguments "-DFSDK_DIR=$projectDir/ext/luna_sdk",
                        "-DANDROID_STL=c++_shared"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "ext/CMakeLists.txt"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes.all {
        // OkHttp
        buildConfigField "long", "OKHTTP_CONNECT_TIMEOUT_SEC", "15"
        buildConfigField "long", "OKHTTP_READ_TIMEOUT_SEC", "30"
        buildConfigField "long", "OKHTTP_WRITE_TIMEOUT_SEC", "30"
        // Location
        buildConfigField "String", "LOCATION_PROVIDER_TYPE", "\"legacy\""
        buildConfigField "long", "LOCATION_UPDATE_INTERVAL_MS", "5000"
        buildConfigField "long", "LOCATION_FASTEST_UPDATE_INTERVAL_MS", "2500"
    }

    buildTypes {
        release {
            applicationIdSuffix ".release"
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BUILD_TAG", "\"release\""
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField "String", "BUILD_TAG", "\"debug\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //implementation "androidx.core:core-ktx:1.0.2"
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"

    AppDependecies.each {
        add(it.configuration, it.dependency)
    }
}
